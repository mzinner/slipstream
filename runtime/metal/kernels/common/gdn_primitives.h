#pragma once

#include "metal/abi/KernelABI.h"

// Ancestor traversal for tree-structured speculative drafting.
// Node 0 is the root / Anchor; its ancestors (< 0) come from conv_state_in.
// For node > 0, parent nibble is read from tree_parents unless tree_parents == 0 (linear fallback).
inline int tree_parent(uint tree_parents, int node) {
  if (node <= 0) return node - 1;
  if (tree_parents == 0) return node - 1;
  return int((tree_parents >> (uint(node) * 4)) & 0xFu);
}

// Four-tap causal convolution of one channel at one token of the command,
// reading the three preceding tokens from the carried state, rounded to bf16
// and gated by SiLU.
inline bfloat gdn_conv_silu(device const bfloat *packed,
                            device const bfloat *conv_state_in,
                            device const bfloat *conv_weights,
                            uint packed_width, uint conv_dim, uint token,
                            uint channel, uint tree_parents = 0) {
  float value = 0.0f;
  int tap_node[4];
  tap_node[3] = int(token);
  tap_node[2] = tree_parent(tree_parents, tap_node[3]);
  tap_node[1] = tree_parent(tree_parents, tap_node[2]);
  tap_node[0] = tree_parent(tree_parents, tap_node[1]);
  for (uint tap = 0; tap < 4; ++tap) {
    int node = tap_node[tap];
    bfloat input = node < 0
                       ? conv_state_in[uint(3 + node) * conv_dim + channel]
                       : packed[uint(node) * packed_width + channel];
    value += float(input) * float(conv_weights[channel * 4 + tap]);
  }
  value = float(bfloat(value));
  return bfloat(value / (1.0f + fast::exp2(-1.44269504089f * value)));
}

// Row `row` of the carried state after consumed_tokens: the last three inputs
// seen, still taken from the incoming state when fewer were consumed.
inline bfloat gdn_conv_carry(device const bfloat *packed,
                             device const bfloat *conv_state_in,
                             uint packed_width, uint conv_dim,
                             uint consumed_tokens, uint row, uint channel) {
  uint source = consumed_tokens + row;
  return source < 3 ? conv_state_in[source * conv_dim + channel]
                    : packed[(source - 3) * packed_width + channel];
}

// The gates of one (token, value head): beta = sigmoid(b) and
// decay = exp(a_scale * softplus(bf16(a + dt_bias))), the softplus rounded to
// bf16 as the reference does.
inline void gdn_write_gates(device const bfloat *packed_row,
                            device const bfloat *dt_bias,
                            device const float *a_scale, uint b_offset,
                            uint a_offset, uint head, device bfloat &beta,
                            device float &decay) {
  float b = float(packed_row[b_offset + head]);
  beta = bfloat(1.0f / (1.0f + fast::exp2(-1.44269504089f * b)));
  bfloat x = bfloat(float(packed_row[a_offset + head]) + float(dt_bias[head]));
  float xf = float(x);
  bfloat softplus =
      bfloat(max(xf, 0.0f) +
             fast::log2(1.0f + fast::exp2(-1.44269504089f * abs(xf))) *
                 0.69314718056f);
  decay = fast::exp(a_scale[head] * float(softplus));
}

// Gated RMSNorm of the recurrent rows, one task per (token, value head) and
// one lane per dimension. Decode walks a lane's rows persistently; prefill
// dispatches one task per threadgroup.
template <uint ValueHeads, uint HeadDim, uint ConvDim, uint Simdgroups = 8>
inline void
gdn_gate_phase(device const bfloat *recurrent, device const bfloat *packed,
               device const bfloat *norm_weight, device bfloat *hidden,
               uint tasks, uint groups, uint packed_width, bool sigmoid_gate,
               threadgroup float *scratch, uint group, uint thread_index,
               uint lane, uint simd_group) {
  constexpr uint ZOffset = ConvDim;
  for (uint task = group; task < tasks; task += groups) {
    uint token = task / ValueHeads;
    uint head = task % ValueHeads;
    ulong base = ulong(task) * HeadDim;
    float value =
        thread_index < HeadDim ? float(recurrent[base + thread_index]) : 0.0f;
    float square_sum = simd_sum(value * value);
    if (lane == 0)
      scratch[simd_group] = square_sum;
    threadgroup_barrier(mem_flags::mem_threadgroup);
    if (thread_index == 0) {
      float total = 0.0f;
      for (uint i = 0; i < Simdgroups; ++i)
        total += scratch[i];
      scratch[0] = rsqrt(total / HeadDim + 1e-6f);
    }
    threadgroup_barrier(mem_flags::mem_threadgroup);
    if (thread_index < HeadDim) {
      bfloat normalized =
          bfloat(value * scratch[0] * float(norm_weight[thread_index]));
      float gate = float(packed[token * packed_width + ZOffset +
                                head * HeadDim + thread_index]);
      float sigmoid = 1.0f / (1.0f + fast::exp2(-1.44269504089f * gate));
      float activation = sigmoid_gate ? sigmoid : gate * sigmoid;
      hidden[base + thread_index] = bfloat(float(normalized) * activation);
    }
    threadgroup_barrier(mem_flags::mem_threadgroup);
  }
}
